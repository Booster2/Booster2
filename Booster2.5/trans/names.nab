module names

imports
	
	include/Booster25

namespaces

	System
	Set
	SetVal
	Class
	Attribute
	Method
	This
	Variable

binding rules // System

	System(s,c):
		defines System s
		scopes Class, Set, SetVal

binding rules // Set

	SetDef(s, values):
		defines Set s
			of type s
		scopes This
		implicitly defines This This()
			of type s
		
	SetVal(value):
		defines SetVal value
			of type ty
		where This() resolves to This this
		and this has type ty

binding rules // Class

	Class(c,extends,members):
		defines Class c
			of type c
		scopes Method, Attribute, This
		implicitly defines This This()
			of type c

	Extension(c):
		imports Attribute, Method from Class c

binding rules // Attribute

	Attribute(a,dec, a-ty):
		defines Attribute a
			of type ty
			where a-ty has type ty

binding rules // Method

	Method(m,body):
		defines Method m
		scopes Variable

binding rules // References

	UniDirectional(s):
		refers to Set s
	otherwise
		refers to Class s

	BiDirectional(class, attr):
		refers to Class class
		refers to Attribute attr in Class class
		
	PathStart(p, prime):
		refers to Attribute p
	otherwise
		refers to Method p
	otherwise
		refers to Class p
	otherwise
		refers to SetVal p
	otherwise
		refers to Variable p

	Input(v):
		refers to Variable v
		
	Output(v):
		refers to Variable v

	Path(path-start, PathComponent(a, prime)):
		refers to Attribute a in Class c
			where path-start has type c
	otherwise
		refers to Method a in Class c
			where path-start has type c

	This(): refers to This This()		

binding rules // Variables
	
	BinRelDefLeftInput  (v,   op, rhs): defines Variable v of type t where rhs has type t
	BinRelDefLeftOutput (v,   op, rhs): defines Variable v of type t where rhs has type t
	BinRelDefRightInput (lhs, op, v  ): defines Variable v of type t where lhs has type t
	BinRelDefRightOutput(lhs, op, v  ): defines Variable v of type t where lhs has type t
			
	Forall(v, expr, body):
		defines Variable v
			of type ty
			where expr has type ty
