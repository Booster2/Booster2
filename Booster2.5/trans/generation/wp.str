module wp

imports
	
	signatures/-
	api/guarded-command
	api/_runtime
	desugar
	api/constraint
	api/attribute
	
	lib/eq
	
	pp
	
	runtime/relations/entries
	runtime/nabl/uri

	names/names
	runtime/types/collect
	
	api/_runtime-constructors

	api/path
	
rules
	
	// Subsets: gets all subsets of a list

	subsets: 		[] -> [[]]		
	subsets:		[x, xs*] -> <union> (<subsets> [xs*], <map(\ p -> <union>([x], p)\)> <subsets> [xs*] )

rules
	
	wp = 
		?ast;
		try(sometd(method-wp(|ast)))

rules
	
	method-wp(|ast): Method(name, decs, Both(c, gc)) -> Method(name, decs, gc'')
		with
			cname := <_nabl-uri ; _nabl-uri-parent ; _nabl-uri-name> name;
	 		all-model-invariants* := <collect-all(?Invariant(_,_))>ast;
			assign-paths* := <map(\Assign(path, expr) -> path \)><collect-all(?Assign(_,_))> gc;
			invariant := <conjoin-constraints><nub><concat><map(\ path -> <filter(invariant-matches-path(|path, cname))> all-model-invariants* \)> assign-paths*;
			(c', gc') := <gc-wp(|ast,And(c, <topdown(try(make-post-state))> invariant))>gc;
			gc'' := Guard(<make-pre-state> c', gc')

rules
	
	gc-wp(|ast,c): [skip] -> (c, Skip())
	
	gc-wp(|ast, c): Assign(path, expr) -> (c', Assign(path, expr)) 
		where
			path' := <make-post-state> path;
			c' := <aliasing-subst(|path', expr)> c


	gc-wp(|ast, c): New(o, cl) -> (c', New(o,cl))
		where	
			c' := <aliasing-subst(|PathStart(cl, Some(Primed())), Union(PathStart(cl, None()), SetExtent([Output(o)])))>c

	gc-wp(|ast, c): Guard(g,p) -> (And(c,c'), Guard( And(c', g), p'))
		where
			(c', p') := <gc-wp(|ast, c)> p	

	gc-wp(|ast, c): Sequence(p1,p2) -> (c'', Sequence(p1',p2'))
		where
			(c', p2') := <gc-wp(|ast, c)> p2;
			(c'', p1') := <gc-wp(|ast, c')> p1

	gc-wp(|ast, c): Parallel(p1,p2) -> ( c'', Parallel(p1',p2'))
		where
			(c', p2') := <gc-wp(|ast, c)> p2;
			(c'', p1') := <gc-wp(|ast, c')> p1

	gc-wp(|ast, c): Choice(p1,p2) -> ( Or(c1,c2), Choice(Guard(c1, p1'),Guard(c2, p2')))
		where
			(c1, p2') := <gc-wp(|ast, c)> p2;
			(c2, p1') := <gc-wp(|ast, c)> p1




	// catch all
	gc-wp(|ast,c): gc -> (c, gc)
		where 
			<debug> "wp failure!!";
			<debug> gc


	invariant-matches-path(|path, C):
			Invariant(iname, invp) -> invp'
	where
			attsMentioned* := <_relation-lookup(|"<hasInv:")> iname;
			<elem> (<_nabl-collect-use;_nabl-uri> path, attsMentioned*);
			A := <_nabl-uri ; _nabl-uri-parent ; _nabl-uri-name> iname;
			Apxs := <filter(\p -> p where <equal>(<_nabl-collect-use;_nabl-uri> p, <_nabl-collect-use;_nabl-uri> path)\)><collect-all(is-path)> invp;
			Cqpxs := <map(\p -> <join-paths> (<path-prefix> path, <path-prefix> p)\)> Apxs;
			invp' := <conjoin-constraints><nub-an><map(contextualize-invariant(|invp))> Cqpxs

	contextualize-invariant(|const) : 
		path {ann} -> Forall("o", path , <literal-subst>(const, [(This(), PathStart("o"{Def(ann')}, None()))]))
		where
			<attr-is-set> path;
			<_nabl-collect-use ; relation-resolve-use ; _nabl-uri-namespace ; ?NablNsAttribute() > path;
			uq := <nabl-uri ; nabl-uri-parent; collect-one(?Unique(_))> ann;
 			ann' := <nabl-uri ; nabl-uri-parent ; nabl-extend-uri(|ID(NablNsVariable(), "o", uq))>ann	


	contextualize-invariant(|const) : 
		path -> <literal-subst>(const, [(This(), path)])
		where
			<not(attr-is-set)> path
	


	// Class names cannot be aliased!
	aliasing-subst(|path, expr): constraint -> <literal-subst> (constraint, [(path, expr)])
		where
			PathStart(p, _) := path;
			<is-class-name> p

	aliasing-subst(|path, expr): Not(a) -> Not(<aliasing-subst(|path, expr)> a)
	aliasing-subst(|path, expr): And(a,b) -> And(<aliasing-subst(|path, expr)> a, <aliasing-subst(|path, expr)> b)
	aliasing-subst(|path, expr): Or(a,b) -> Or(<aliasing-subst(|path, expr)> a, <aliasing-subst(|path, expr)> b)
	aliasing-subst(|path, expr): Implies(a,b) -> Implies(<aliasing-subst(|path, expr)> a, <aliasing-subst(|path, expr)> b)
	aliasing-subst(|path, expr): Forall(i, expr',a) -> Forall(i, expr', <aliasing-subst(|path, expr)> a)
	aliasing-subst(|path, expr): Exists(i, expr',a) -> Exists(i, expr', <aliasing-subst(|path, expr)> a)


	aliasing-subst(|path, expr): constraint -> constraint'
		where
			path-uri := <_nabl-collect-use;_nabl-uri> path;
			// this will collect duplicate paths - n is the same as n, but their locations are different.
			// nub cannot fix this - do it some other way!
			matching-paths* := <nub-an> <filter(\ p -> p where <equal>(<_nabl-collect-use;_nabl-uri> p , path-uri); <is-post-state> p \)> <collect-all-paths> constraint;
			
			constraint' := <conjoin-constraints> <map(\s -> <generate-constraint> (s, <diff>(matching-paths*, s), path, expr, constraint)\)><subsets> matching-paths*
			
			
	literal-subst: (constraint, substs) -> constraint''
		where
			constraint' := <bottomup(try(path-add-this))> constraint;
			substs' := <map(\ (t,e) -> ( <remove-all-annotations><bottomup(try(path-add-this))> t , e) \)> substs;
			constraint'' := <bottomup(try(\ trm -> <lookup> (<remove-all-annotations><path-add-this> trm  , substs')\))> constraint'
			
	remove-all-annotations = bottomup(try(\ t -> t {}\)) 
		
		
		
//	generate-constraint: ([], neg, path, expr, constraint) -> constraint

	generate-constraint: (pos, [], path, expr, constraint) -> Implies(constraint1, constraint3)
		where
			constraint1 := <conjoin-constraints> <map(\ p -> Equal(<path-prefix> p, <path-prefix> path) \)> pos;
			constraint3 := <literal-subst> (constraint, <map(\ p -> (p, expr)\)> pos)

	generate-constraint: (pos, neg, path, expr, constraint) -> Implies(And(constraint1, constraint2), constraint3)
		where
			constraint1 := <conjoin-constraints> <map(\ p -> Equal(<path-prefix> p, <path-prefix> path) \)> pos;
			constraint2 := <conjoin-constraints> <map(\ q -> NotEqual(<path-prefix> q, <path-prefix> path) \)> neg;
			constraint3 := <literal-subst> (constraint, <map(\ p -> (p, expr)\)> pos)
			 
rules
	


	
rules

