module trans/generation/sql

imports
	
	include/Booster25
	trans/api/attribute
	trans/api/method
	trans/api/type
	trans/api/path
	trans/api/_runtime
	
	
rules
	
	generate-sql: x@System(name, cs) -> StatementList([
		DropDatabase(name),
		CreateDatabase(name),
		UseDatabase(name),
		<metadata-tables> name,
		StatementList(<collect-all(sql-metadata)> x),
 		StatementList(<collect-all(sql-set-def)> x),
		StatementList(<collect-all(sql-classes)> x),
		StatementList(<collect-all(sql-set-atts)> x),
		StatementList(<collect-all(sql-assoc-classes)> x),
		StatementList(<collect-all(sql-method)> x)
		])
	
	sql-metadata:
		SetDef(setname, values) ->
			Insert(None(), None(), Some(Into()), "_Meta_Sets", 
						  	[ColumnName("setName"),	ColumnName("tablename"),	ColumnName("columnName")], 
								[String(setname),				String(setname),					String(setname)], 
			None()) 


	sql-metadata: Class(name, exts, comps) -> 
			Insert(None(), None(), Some(Into()), "_Meta_Classes", [ColumnName("className"), ColumnName("tablename")], [String(name),String(name)], None())

	sql-metadata: a@Attribute(name, decs, type) -> 
			Insert(None(), None(), Some(Into()), 
						"_Meta_Attributes", 
						[ColumnName("class"),	ColumnName("attName"),	ColumnName("primType"),	ColumnName("typeMultiplicity"),	ColumnName("oppAttName"),	ColumnName("className"),	ColumnName("setName"),	ColumnName("direction"),	ColumnName("tableName"),	ColumnName("isId")],
						[String(classname),		String(name),						String(primType),				String(typeMultiplicity),				String(opp-aname),				String(opp-cname),				String(setName),				String(direction),				String(tname),						isId], 
						None())
			where
				classname := <attr-parent> name;
				primType := <prim-type-string> name;
				typeMultiplicity := <multiplicity-string> name;
				setName := <set-name> name;
				direction := <get-dir> name;
				isId := <is-id> a;
				opp-aname := <attr-get-inverse <+ !""> name;
				opp-cname := <attr-parent <+ !""> opp-aname;
				tname := <concat-strings> [<attr-parent> name, "_", name]
						 
	sql-metadata:
		m@Method(name, gc) ->
			StatementList([Insert(None(), None(), Some(Into()), "_Meta_Methods", 
						[ColumnName("class"),ColumnName("methodName"),ColumnName("isObjectMethod"),ColumnName("html")], 
						[String(classname),String(name),isObjectMethod, String(html)], None()),
						inParamStmts*,
						outParamStmts*
						])
		where
			classname := <method-parent>name;
			isObjectMethod := <is-obj-method> m;
			html := <string-replace(|"'", "''")> "";
			[inParamStmts*] := <map(metadata-param(|m))><method-inputs> m;
			[outParamStmts*] := <map(metadata-param(|m))><method-outputs> m
						
						
	metadata-param(|m): x ->
			Insert(None(), None(), Some(Into()), "_Meta_Method_Params", 
						[ColumnName("class"), 	ColumnName("methodName"), 	ColumnName("paramName"),	ColumnName("paramType"),	ColumnName("paramMultiplicity"),	ColumnName("paramInOut"),	ColumnName("paramClassName"), ColumnName("paramSetName")], 
						[String(cname), 				String(mname),							String(pname),						String(ptname), 					String(mult),											String(inout), 						String(classname), 						String(setname)], 
						None())
			where
				Method(mname, gc) := m;
				cname := <_nabl-uri ; _nabl-uri-parent ; _nabl-uri-name> mname;
				pname := < ?Input(<id>) <+ ?Output(<id>) <+ (?This() ; !"this")> x;
				ptname := <prim-type-string> x;
				mult := <multiplicity-string> x;
				inout := <in-out-string> x;
				classname := <class-name> x;
				setname := <set-name> x 


	sql-classes: Class(name, exts, comps) -> 
			<create-table>(name, <concat-strings>[name, "Id"], Int(), Some(ColumnNotNullable()), None(),Some(AutoIncrement()), None())

	
	// Mandatory or optional basic types get turned into single columns
	sql-classes: Attribute(name, decs, type) -> CreateColumn(tname, name, sqltype, nullable, default, None(), None(), None())
		where
			not(<attr-is-set> name);
			sqltype := <_get-type ; is-prim-type ; sql-type> name;
			tname := <attr-parent> name;
			default := <column-default> name;
			nullable := <column-nullable> name

	// Mandatory or optional enum types get turned into single columns with references
	sql-classes: Attribute(name, decs, type) -> CreateColumn(tname, name, Varchar("100"), nullable, default, None(), None(), Some(Reference(name, setname, [ColumnName(setname)])))
		where
			not(<attr-is-set> name);
			<attr-is-enum> name;
			setname := <set-name> name;
			tname := <attr-parent> name;
			default := <column-default> name;
			nullable := <column-nullable> name


	// Set-valued attributes get turned into tables
	sql-set-atts: Attribute(name, decs, type) -> 
		StatementList([
			<create-table>(tname, <concat-strings>[tname, "Id"], Int(), Some(ColumnNotNullable()), None(),Some(AutoIncrement()), None()),
			CreateColumn(tname, firstcolname, Int(), Some(ColumnNotNullable()), None(), None(), None(), Some(Reference(firstcolname, parent-name, [ColumnName(<concat-strings>[parent-name, "Id"])]))),
			CreateColumn(tname, name, sqltype, Some(ColumnNotNullable()), None(), None(), None(), None())
		])
		where
			<attr-is-set> name;
			sqltype := <_get-type ; is-prim-type ; sql-type> name;
			parent-name := <attr-parent> name;
			tname := <concat-strings>[parent-name, "_", name];
			firstcolname := <concat-strings>[parent-name, "Id"]



	// Set-valued attributes get turned into tables
	sql-set-atts: Attribute(name, decs, type) -> 
		StatementList([
			<create-table>(tname, <concat-strings>[tname, "Id"], Int(), Some(ColumnNotNullable()), None(),Some(AutoIncrement()), None()),
			CreateColumn(tname, firstcolname, Int(), Some(ColumnNotNullable()), None(), None(), None(), Some(Reference(firstcolname, parent-name, [ColumnName(<concat-strings>[parent-name, "Id"])]))),
			CreateColumn(tname, name, Int(), Some(ColumnNotNullable()), None(), None(), None(), Some(Reference(name, setname, [ColumnName(setname)])))
		])
		where
			<attr-is-set> name;
			<attr-is-enum> name;
			setname := <set-name> name;
			parent-name := <attr-parent> name;
			tname := <concat-strings>[parent-name, "_", name];
			firstcolname := <concat-strings>[parent-name, "Id"]
	

	// Set-valued attributes get turned into tables
	sql-set-atts: Attribute(name, decs, type) -> 
		StatementList([
			<create-table>(tname, <concat-strings>[tname, "Id"], Int(), Some(ColumnNotNullable()), None(),Some(AutoIncrement()), None()),
			CreateColumn(tname, firstcolname, Int(), Some(ColumnNotNullable()), None(), None(), None(), Some(Reference(firstcolname, parent-name, [ColumnName(<concat-strings>[parent-name, "Id"])]))),
			CreateColumn(tname, name, Int(), Some(ColumnNotNullable()), None(), None(), None(), Some(Reference(name, classname, [ColumnName(<concat-strings>[classname, "Id"])])))
		])
		where
			<attr-is-set> name;
			<attr-is-unidir> name;
			classname := <class-name> name;
			parent-name := <attr-parent> name;
			tname := <concat-strings>[parent-name, "_", name];
			firstcolname := <concat-strings>[parent-name, "Id"]
			
		
	
	sql-assoc-classes: Attribute(name, decs, type) -> StatementList([
		<create-table>(tname, <concat-strings>[tname, "Id"], Int(), Some(ColumnNotNullable()), None(), Some(AutoIncrement()), None()),
		CreateColumn(tname, cname1 , Int(), Some(ColumnNotNullable()), None(), None(), None(), Some(Reference(cname1, <attr-parent> name, [ColumnName(<concat-strings>[<attr-parent> name, "Id"])]))),
		CreateColumn(tname, cname2 , Int(), Some(ColumnNotNullable()), None(), None(), None(), Some(Reference(cname2, opp-cname, [ColumnName(<concat-strings>[opp-cname, "Id"])])))
	])
		where
			opp-aname := <attr-get-inverse> name;
			opp-cname := <attr-parent>opp-aname;
			tname := <concat-strings> [<attr-parent> name, "_", name];
			cname1 := <concat-strings> [<attr-parent> name, "_", name];
			cname2 := <concat-strings> [opp-cname, "_", opp-aname]
			 
			
	sql-set-def: SetDef(name, values) -> StatementList([
		DropTable(name),
		CreateTable(name, PrimaryKeyColumn(name, Varchar("100"), None(), None(), None(), None())),
		StatementList(<map(insert-value(|name))> values)
	])	
	
	
	insert-value(|name): value ->
		Insert(None(), None(), Some(Into()), name, [ColumnName(name)], [String(value)], None())
		
		
		
	sql-method:  m@Method(mname, gc) -> 
		StatementList([
				DropProcedure(pname),
				CreateProcedure(pname, params, stmt)
			])
			where
				pname := <concat-strings>[<method-parent> mname, "_", mname];
				params := <get-sql-params> m;
				stmt := <sql-gc> gc
					
	sql-gc: Skip() -> VariableAssign("dummySkip", Int("1"))


	sql-gc: Assign(p, expr) -> 
			StatementList([VariableAssign(pn', expr'), 
										 Update([], TableName(tname),[UpdateSet(attname, LocalVariable(pn'))],Some(Where(expr'')),None(),None())])
			where
			not(<attr-is-set> p);
			attname := <path-suffix-name> p;
			sqltype := <_get-type ; is-prim-type ; sql-type> p;
			pn' := <path-to-var-name> p;
			tname := <path-prefix ; class-name> p;
			expr' := <sql-expr> expr;
			expr'' := <path-where> p
	
	sql-gc: Guard(const, gc) -> IfThen(const', stmt,[],None())
		where
			const' := <sql-const> const;
			stmt := <sql-gc> gc

	sql-gc: gc -> VariableAssign("dummySkip", Int("1"))
		where
			<debug> "Cannot convert to sql";
			<debug> gc





	sql-const: True() -> True()
	sql-const: False() -> False()
	
	sql-const: Not(const) -> Not(<sql-const> const)
	sql-const: And(c1, c2) -> And(<sql-const>c1, <sql-const>c2)
	sql-const: Or(c1, c2) -> Or(<sql-const>c1, <sql-const>c2)
	
	sql-const: GreaterThan(e1, e2) -> GreaterThan(<sql-expr>e1, <sql-expr>e2)
	
	sql-expr: BasicValue(Integer(n)) -> Int(n)
	
	sql-expr: Input(n) -> SystemVariable(<concat-strings>[n, "_in"])
	sql-expr: Output(n) -> SystemVariable(<concat-strings>[n, "_out"])
	sql-expr: This() -> SystemVariable("this")
	

	
	metadata-tables = !StatementList([
			<meta-classes-table>,
			<meta-sets-table>,		
			<meta-attributes-table>,
			<meta-methods-table>,
			<meta-method-params-table>
	])

	meta-classes-table = !StatementList([
			<create-table>("_Meta_Classes", "id", Int(), None(), None(), Some(AutoIncrement()), None()),
			CreateColumn("_Meta_Classes", "className", Varchar("200"), None(), None(), None(), None(), None()),
			CreateColumn("_Meta_Classes", "tableName", Varchar("200"), None(), None(), None(), None(), None())
	])

	meta-sets-table = !StatementList([
			<create-table>("_Meta_Sets", "id", Int(), None(), None(), Some(AutoIncrement()), None()),
			CreateColumn("_Meta_Sets", "setName", Varchar("200"), None(), None(), None(), None(), None()),
			CreateColumn("_Meta_Sets", "tableName", Varchar("200"), None(), None(), None(), None(), None()),
			CreateColumn("_Meta_Sets", "columnName", Varchar("200"), None(), None(), None(), None(), None())
	])

	meta-attributes-table = !StatementList([
			<create-table>("_Meta_Attributes", "id", Int(), None(), None(), Some(AutoIncrement()), None()),
			CreateColumn("_Meta_Attributes", "class", Varchar("200"), None(), None(), None(), None(), None()),
			CreateColumn("_Meta_Attributes", "attName", Varchar("200"), None(), None(), None(), None(), None()),
			CreateColumn("_Meta_Attributes", "primType", Varchar("200"), None(), None(), None(), None(), None()),
			CreateColumn("_Meta_Attributes", "typeMultiplicity", Varchar("200"), None(), None(), None(), None(), None()),
			CreateColumn("_Meta_Attributes", "className", Varchar("200"), None(), None(), None(), None(), None()),
			CreateColumn("_Meta_Attributes", "setName", Varchar("200"), None(), None(), None(), None(), None()),
			CreateColumn("_Meta_Attributes", "direction", Varchar("200"), None(), None(), None(), None(), None()),
			CreateColumn("_Meta_Attributes", "tableName", Varchar("200"), None(), None(), None(), None(), None()),
			CreateColumn("_Meta_Attributes", "oppAttName", Varchar("200"), None(), None(), None(), None(), None()),
			CreateColumn("_Meta_Attributes", "isId", Bit(), None(), None(), None(), None(), None())
	])

	meta-methods-table = !StatementList([
			<create-table>("_Meta_Methods", "id", Int(), None(), None(), Some(AutoIncrement()), None()),
			CreateColumn("_Meta_Methods", "class", Varchar("200"), None(), None(), None(), None(), None()),
			CreateColumn("_Meta_Methods", "methodName", Varchar("200"), None(), None(), None(), None(), None()),
			CreateColumn("_Meta_Methods", "isObjectMethod", Bit(), None(), None(), None(), None(), None()),
			CreateColumn("_Meta_Methods", "html", Text(), None(), None(), None(), None(), None())
	])


	meta-method-params-table = !StatementList([
			<create-table>("_Meta_Method_Params", "id", Int(), None(), None(), Some(AutoIncrement()), None()),
			CreateColumn("_Meta_Method_Params", "class", Varchar("200"), None(), None(), None(), None(), None()),
			CreateColumn("_Meta_Method_Params", "methodName", Varchar("200"), None(), None(), None(), None(), None()),
			CreateColumn("_Meta_Method_Params", "paramName", Varchar("200"), None(), None(), None(), None(), None()),
			CreateColumn("_Meta_Method_Params", "paramType", Varchar("200"), None(), None(), None(), None(), None()),
			CreateColumn("_Meta_Method_Params", "paramMultiplicity", Varchar("200"), None(), None(), None(), None(), None()),
			CreateColumn("_Meta_Method_Params", "paramInOut", Varchar("20"), None(), None(), None(), None(), None()),
			CreateColumn("_Meta_Method_Params", "paramClassName", Varchar("200"), None(), None(), None(), None(), None()),
			CreateColumn("_Meta_Method_Params", "paramSetName", Varchar("200"), None(), None(), None(), None(), None())
			
	])


	path-where:
		p -> Equal(expr, ColumnName(<concat-strings>[cname, "Id"]))
		where 
			not(<attr-is-set> p);
			cname := <path-prefix ; class-name> p;
			expr := <path-prefix ; sql-expr> p



	path-where:
		Path(p {Reference(cname)}, PathComponent(attname, None())){t}
			-> Equal(expr, ColumnName(<concat-strings>[cname, "_",attname]))
		where 
			expr := <sql-expr> p
		
	path-where: Output(n) -> True()

	path-where: This() -> True()

	path-where: ThisPrimed() -> True()


rules
	
			create-table: (name, pkname, pktype, pknullable, pkdefault, pkextra, pkreference)	
				-> 		
				StatementList([
					DropTable(name),
					CreateTable(name, PrimaryKeyColumn(pkname, pktype, pknullable, pkdefault, pkextra, pkreference))
				])

rules