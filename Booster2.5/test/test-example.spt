/**
* Unit tests for the Booster25 language.
*/
module test-example

language Booster25

setup Common [[
  module test
]]

test Simple entity [[
  entity User {
    name  : String
    other : User
  }
]] 0 errors

test Resolve type fails [[
  entity User {
    name  : [[Strin]]
    other : [[Use]]
  }
]] 2 errors

test Duplicate entity fails [[
  [[entity User {}]]
  [[entity User {}]]
]] 2 errors

test Duplicate property fails [[
  entity User {
  	[[name : String]]
  	[[name : String]]
  }
]] 2 errors

test Resolve type [[
  entity [[User]] {}
  
  entity Owner {
    owns : [[User]]
  }
]] resolve #2 to #1

test Complete primitive type [[
  entity User {
    name : [[Str]]
  }
]] complete to "String"

test Complete user type [[
  entity User {
    owner : [[Us]]
  }
]] complete to "User"
