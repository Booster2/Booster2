module Triple

imports Common


context-free start-symbols

  Statement

context-free syntax

Statement.Prefixes = 
<@prefix rr: \<http://www.w3.org/ns/r2rml#\>.
@prefix rdf: \<http://www.w3.org/1999/02/22-rdf-syntax-ns#\>.
@prefix rdfs: \<http://www.w3.org/2000/01/rdf-schema#\>.
@prefix xsd: \<http://www.w3.org/2001/XMLSchema#\>.
@prefix map: \<#\>.
@prefix <ID>: \<booster2/<ID>#\>.
>

Statement.Prefix = <@prefix <ID>: \<<URL>\>.>


Statement.StatementList = <<{Statement "\n"}*>

>

Statement.CreateMapping = <map:<ID>
   
   rr:logicalTable [ rr:tableName "<DBdotView>"; ];
   
   <Subject>

   <AttrMap>
   .
   
>

Statement.Simple = <map:<ID>
    rr:logicalTable [ rr:tableName "<DBdotView>"; ];
    
    <AttributeSubject>
   .
   
>

AttributeSubject.AttributeSubject = 
<rr:subjectMap [rr:template "booster2/<ID>/{`<ID>_<ID>`}";  
                rr:class <ID>:<ID>; 
	];>

Subject.Subject = 
<rr:subjectMap [rr:template "booster2/<ID>/{`<ID>`}";  
                rr:class <ID>:<ID>; 
	];>
	
Subject.MultiSubject = 
<rr:subjectMap [rr:template "booster2/<ID>/{`<ID>`}";  
                rr:class <ID>:<ID>;
                <SubjectList> 
	];>
	
SubjectList.SubjectList = <<{SubjectMap"\n"}*>>

SubjectMap.SubjectMap = <rr:class <ID>:<ID>;>

SubjectMap.SubjectMap = <rr:class <URL>;>

										
AttrMap.AttrMapList = <<{AttrMap "\n\n"}*>>
										
										
AttrMap.AttrMap = 
<rr:predicateObjectMap [
        rr:predicate <DBColName>;
        rr:objectMap [ rr:column "`<ColumnName>`"; ];
    ];
>

AttrMap.MultiAttrMap = 
<rr:predicateObjectMap [	
        <Predicate>
        <PredicateList>
        rr:objectMap [ rr:column "`<ColumnName>`"; ];
    ];
>
PredicateList.PredicateList = <<{Predicate"\n"}*>>

Predicate.Predicate = <rr:predicate <ID>:<ID>;>

Predicate.Predicate = <rr:predicate <URL>;>

AttrMap.AttrMapRel = 
<rr:predicateObjectMap [
        rr:predicate <DBColName>; 
        rr:objectMap [
            rr:parentTriplesMap map:<ColumnName>; 
            rr:joinCondition [ rr:parent "`<ColumnName>`";  rr:child "`<ColumnName>`"; ]; 
        ];
    ];
>


DBdotView.DBdotView = <`<ID>`.`<ID>_<ID>`> 

DBColName.DBColName = <<ID>:<ID>> 
ColumnName.ColumnName = <<ID>> 
lexical syntax

  URL = ~[\ \t\n\r\,\/\:]+ "://" ~[\ \t\n\r]+
  PREFIXID = ~[\ \t\n\r\,\/\:]+  
