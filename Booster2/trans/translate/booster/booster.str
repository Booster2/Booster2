module translate/booster/booster

imports
  include/Booster2

  library/lookupTable
  library/basicTypes
  library/binOp
  library/unOp
  library/binRel 
  
  translate/helpers

 rules
 	
/* output-to-booster:
*/


substs: s -> <foldout(\ (inp, expr) -> $[[<expression> inp] = [<expression> expr]]\)> (s, ",")

outputAttributes = (\ classname -> <foldout(attribute)>(<filter(\ ((cname, aname), adef) -> ((cname, aname), adef) where <eq>(cname, classname)\)><LookupTable>"Attribute", "\r") \)
outputMethods = (\ classname -> <foldout(method)>(<filter(\ ((cname, mname), mdef) -> ((cname, mname), mdef) where <eq>(cname, classname)\)><LookupTable>"Method", "\r") \)

// workflow begin
outputSeqWorkflows = (\ classname -> <foldout(seqwf)>(<filter(\ ((cname, aname), adef) -> ((cname, aname), adef) where <eq>(cname, classname)\)><LookupTable>"SeqWorkflows", "\r") \)
outputParWorkflows = (\ classname -> <foldout(parwf)>(<filter(\ ((cname, aname), adef) -> ((cname, aname), adef) where <eq>(cname, classname)\)><LookupTable>"ParWorkflows", "\r") \)
// workflows end


	output-to-booster:
	 	system
	 		-> $[system [<LookupTable> "Name"] 
	 			 ["\r\r"] 
	 			 [<foldout(setdef)> (<LookupTable> "SetDef", "\r\r")]
	 			 ["\r\r"]
	 			 [<foldout(class)> (<LookupTable> "Class", "\r\r")] ]

		
	setdef:
		(name, values)
			-> $[["\t"]set [name] { [<foldout(set-value)> (values, ", ")] }]
	
	set-value:
		name -> $[[name]]




	class:
		(classname, (immsubclasses, allsubclasses, atts, meths, invs, wfs))
			-> $[["\t"]class [classname] [<extends> (immsubclasses)] { [<attributes-section> (classname, atts)][<invariants-section> (classname, invs)][<methods-section> (classname, meths)][<workflows-section> (classname, wfs)]["\r\t"]}]
	
	attributes-section:
		(classname, []) -> $[]
		
	attributes-section:
		(classname, atts) -> $[["\r\t\t"]attributes["\r"][<outputAttributes> classname]]

	invariants-section:
		(classname, []) -> $[]
		
	invariants-section:
		(classname, invs) -> $[["\r\t\t"]invariants["\r"] [<foldout(invariant)> (invs, "\r")]]	
	
	methods-section:
		(classname, []) -> $[]
		
	methods-section:
		(classname, meths) -> $[["\r\t\t"]methods["\r"] [<outputMethods> classname]]	

	workflows-section:
		(classname, ([],[])) -> $[]
		
	workflows-section:
		
		(classname, wfs) -> $[["\r\t\t"]workflows["\r"] [<outputSeqWorkflows> classname] ["\r"] [<outputParWorkflows> classname]] where log(|Error(), "Workflows", wfs)	
	

				
	extends:
		[] -> $[]
		
	extends:
		extends -> $[extends [<foldout(classname)>(extends, ", ")]]
					
	classname:
		name -> $[[name]]
					
	attribute:
		((classname, attname), (decs, type, opposite, minmult, maxmult, tname)) 
			-> $[["\t\t\t"][attname] [<foldout(decorator)>(decs, ", ")] : [<type>(type, opposite, minmult, maxmult)] ]
			//[<output-to-booster-attribute-type>(classname, attname)]]
	
	// workflow begin
	seqwf : ((classname, sname), expr) -> $[["\t\t\t"]seq [sname] : [<seqWfNotation> expr]]
	
	seqWfNotation : Skip() -> "SKIP"
	seqWfNotation : Prefix(ga,wf) -> $[[<guardedActionNotation> ga] -> [<seqWfNotation> wf]]
	seqWfNotation : Choice(ga1,wf1,ga2,wf2) -> $[([<guardedActionNotation> ga1] -> ([<seqWfNotation> wf1]) ["[]"] [<guardedActionNotation> ga2] -> ([<seqWfNotation> wf2]))]
	seqWfNotation : Wait(list,wf) -> $[WAIT{[<foldout(id)> (list, ",") ]} -> [<seqWfNotation> wf]]
	seqWfNotation : WorkflowReference(n) -> n
	
	seqWfNotation : t -> "???"
	where log(|Error(),"notation not known: ",t)
	
	// precondition not available
	parwf : ((classname, sname), expr) -> $[["\t\t\t"]par [sname] : [<parWfNotation> expr]]
	where
		not (<lookup> ((classname,sname),<LookupTable> "PrecondParWorkflows"))
	
	// precondition available
	parwf : ((classname, sname), expr) -> $[["\t\t\t"]par [<constraint> cond] & [sname] : [<parWfNotation> expr]]
	where
		cond := <lookup> ((classname,sname),<LookupTable> "PrecondParWorkflows")
		
	parWfNotation: [] -> ""
	parWfNotation: [wfr] -> wfr
	parWfNotation: [wfr|list] -> $[[wfr] ||| [<parWfNotation> list]]
	
	parWfNotation : t -> "???"
	where log(|Error(),"notation not known: ",t)
	
	
	
	guardedActionNotation: GA(guard,methodRef) -> $[[<guardNotation> guard] & [<constraint> methodRef]]
	
	guardedActionNotation: t -> "???"
	where log(|Error(),"notation not known: ",t)
	
	guardNotation: Normal(constr) -> <constraint> constr
	guardNotation: Delayed(constr) -> $[Delayed([<constraint> constr])]
	
	guardNotation: t -> "???"
	where log(|Error(),"notation not known: ",t)
	
	// workflow end
	
	decorator:
		Identity() -> $[(ID)]
		
		
	type: 
		(BasicType(t), opposite, minmult, maxmult)	
			-> <get-type-booster-notation> t

	type:
		(Optional(BasicType(t)), opposite, minmult, maxmult)
			-> ${[ { <get-type-booster-notation> t } ]}
				
	type:
		(Set(BasicType(t)), opposite, minmult, maxmult)
			-> ${set ( { <get-type-booster-notation> t } )  [{minmult} .. {maxmult}]}
			
	type:
		(Reference(name), opposite, minmult, maxmult)
			-> $[ [name] [<opposite>opposite] ]

	type:
		(Optional(Reference(name)), opposite, minmult, maxmult)
			-> ${[ {name} {<opposite>opposite} ]}

	type:
		(Set(Reference(name)), opposite, minmult, maxmult)
			-> ${set ( { name }{<opposite>opposite} )  [{minmult} .. {maxmult}]}

	opposite:
		(name, name') -> $[ . [name']]

	opposite:
		() -> $[]

	invariant:
		(constraint)
			-> $[["\t\t\t"][<constraint> constraint]]

	
	method:
		((classname, methodname), (constraint, guardedCommand, exts, done, inputs, outputs))
			-> $[["\t\t\t"][methodname] { [<guardedCommand> guardedCommand] }]
		where
			<not(equal)>(guardedCommand, ())

	method:
		((classname, methodname), (constraint, guardedCommand, exts, done, inputs, outputs))
			-> $[["\t\t\t"][methodname] { [<constraint> constraint] }]

	guardedCommand:
		Skip() -> $[skip]

	guardedCommand:
		New(Output(o), cl) -> $[new [o]! : [<expression> cl]]
			
	guardedCommand:
		Assign(path, expression) -> $[[<expression> path] := [<expression> expression]]								
			
	guardedCommand:
		Parallel (gc1, gc2) -> $[ ([<guardedCommand> gc1]) || ([<guardedCommand> gc2])]
			
	guardedCommand:
		Choice (gc1, gc2) -> ${({<guardedCommand> gc1}) [] ({<guardedCommand> gc2})}
			
	guardedCommand:
		Sequence (gc1, gc2)
			-> $[([<guardedCommand> gc1]) ; ([<guardedCommand> gc2])]
			
	guardedCommand:
		Guard (constraint, gc)
			-> $[ [<constraint> constraint] --> ( [<guardedCommand> gc] )]
				
										
	constraint: 
		True() -> $[true]
			
	constraint: 
		False() -> $[false]
			
	constraint:
			Not(constraint) -> $[not ([<constraint> constraint])]	
	
	constraint: 
		And(constraint1, constraint2) -> $[([<constraint> constraint1] & [<constraint> constraint2])]

	constraint: 
		Or(constraint1, constraint2) -> $[([<constraint> constraint1] or [<constraint> constraint2])]

	constraint: 
		Then(constraint1, constraint2) -> $[([<constraint> constraint1] ;[<constraint> constraint2])]

	constraint: 
		Implies(constraint1, constraint2) -> $[([<constraint> constraint1] => [<constraint> constraint2])]

	constraint: 
		Exists(name, expr, constraint) -> $[(exists [name] : [<expression> expr] @ ([<constraint> constraint]))]

	constraint: 
		Forall(name, expr, constraint) -> $[(forall [name] : [<expression> expr] @ ([<constraint> constraint]))] 										
										
	constraint:
		BinRel(expr1, rel, expr2) -> $[[ <expression> expr1] [<get-binrel-booster-notation> rel] [<expression> expr2]]
		
	constraint:
		MethodReference(path, substs) -> $[[<expression> path] ( [<substs> substs]) ]


	expression:
		UnOp(unop,expr1) -> $[[<get-unop-booster-notation> unop] ( [<expression> expr1] ) ]

	expression:
		BinOp(expr1, op, expr2) -> $[[<expression> expr1] [<get-binop-booster-notation> op] [<expression> expr2]]

	expression:
		BasicValue(Integer(i)) -> $[[i]]

	expression:
		BasicValue(String(s)) -> $[[s]]
			
	expression:
		TypeExtent(t) -> <get-type-booster-notation> t 
			
	expression:
		Null() -> $[null]
			
	expression:
		Input(inputname) -> $[[inputname]?]
			
	expression:
		Output(outputname) -> $[[outputname]!]
			
	expression:
		This() -> $[this]
			
	expression:
		ThisPrimed() -> $[this']

	expression:
		Path(a,b) -> $[[<expression> a].[<expression> b]]
			
	expression:
		PathComponent(name, None()) -> $[[name]]

	expression:
		PathComponent(name, Some(Primed())) -> $[[name]']

	expression:
		PathStart(st, None()) -> $[[st]]
			
	expression:
		PathStart(st, Some(Primed())) -> $[[st]']

	expression:
		SetExtent(exprs) -> $[{[<foldout(expression)>(exprs, ", ")]}]
		
		
