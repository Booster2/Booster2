module translate/sql/sql

imports
  include/Booster2
  
  translate/helpers

rules 

// Main editor interface (defined by editor/Booster-Builders and -References.esv)
  
      

	pp-install:
	 	Schema(name, tables, procedures, StatementList(statements))
	 		-> $[drop database if exists `[name]`;
				 create database `[name]`;
	 		     use `[name]`;
	 		     SET autocommit=0;
	 		[<map(pp-install-table)> tables]
	 		[<map(pp-install-procedure)> procedures]
			[<map(proc-stmt)> statements]
			[<standard-stuff> name]]
	 		
	 pp-install-table:
	 	Table(name, [column, columns*])
	 	
	 		-> $[drop table if exists `[name]`;
	 			 create table `[name]` ([<pp-install-column> column]);
	 			[<map((\ c -> (name, c) \) ; pp-install-columns)> [columns*]]]
	 			


	 pp-install-columns:
	 	(name, coldef)
	 		-> $[ alter table `[name]`
	 				add column [<pp-install-column> coldef];
	 			]
	 
	pp-install-column:
		Column(columnName, default, nullable, type, extra, key, reference)
			->  $[[columnName] [<column-type> type] [<column-null> nullable] [<column-default> default] [<column-extra> extra] [<column-key> key] [<reference> (columnName, reference)]]
			
	reference:
		(columnname, None()) -> []

	reference:
		(columnname, Some(Reference(name, names)))
			-> $[, add foreign key (`[columnname]`) references `[name]` ( [<foldout(column-name)> (names, ", ")])] 
	 		
	 pp-install-procedure:
	 	Procedure(name, inputs, DeclareList(declares), StatementList(statements))
	 		-> $[ drop procedure if exists [name];
	 			  delimiter //
	 			  create procedure `[name]` ( [<foldout(proc-param)> (inputs, ",")])
	 				begin
	 				declare exit handler for sqlwarning, sqlexception, not found 
	 				begin
					rollback;
					end; 
					start transaction;
	 			    [<foldout(declare-statement)> (declares, " ")]
	 			    [<map(proc-stmt)> statements]
	 			    commit;
	 			  	end //
	 			  delimiter ;]

	 pp-install-procedure:
	 	Function(name, inputs, retType, DeclareList(declares), StatementList(statements))
	 		-> $[ drop function if exists `[name]`;
	 			  delimiter //
	 			  create function `[name]` ( [<foldout(proc-param-without-inout)> (inputs, ",")])
	 			  	returns [<column-type> retType]
	 				begin
	 			    [<foldout(declare-statement)> (declares, " ")]
	 			    [<map(proc-stmt)> statements]
	 			  	end //
	 			  delimiter ;]
 	 
			
	 column-type:
	 	Varchar(n)
	 		-> $[varchar([n])]

	 column-type:
	 	Bit()
	 		-> $[bit]

	 column-type:
	 	Int()
	 		-> $[int]

	 column-type:
	 	Decimal()
	 		-> $[decimal(65,30)]
	 				
	 column-type:
	 	Datetime()
	 		-> $[datetime]

	 column-type:
	 	Date()
	 		-> $[date]

	 column-type:
	 	Time()
	 		-> $[time]

	 column-type:
	 	Text()
	 		-> $[text]

	 column-type:
	 	other
	 		-> $[something]
	 			
	 column-null:
	 	False() -> ""
	 	
	 column-null:
	 	True() -> "null"
	 	
	 column-default:
	 	"" -> ""
	 
	 column-default:
	 	"null" -> $[default null]

	 column-default:
	 	str -> $[default "[str]"]
	 	
	 column-extra:
	 	AutoIncrement() -> $[auto_increment]

	 column-extra:
	 	NoExtra() -> ""
	 	

	column-key:
			Primary() -> $[primary key]
			
	column-key:		
			Unique() -> $[unique]

	column-key:
		Multiple() -> $[multiple]
	
	column-key:
		NoKey() -> $[]	
		
	proc-param-without-inout:
		Input("this", type)
			-> $[this [<column-type> type]]

	proc-param-without-inout:
		Input(name, type)
			-> $[[name]_in [<column-type> type]]
		
	proc-param:
		Input("this", type)
			-> $[in this [<column-type> type]]

	proc-param:
		Input(name, type)
			-> $[in [name]_in [<column-type> type]]
		
	proc-param:
		Output(name, type)
			-> $[out [name]_out [<column-type> type]]
			
	proc-param:
		InOut(name, type)
			-> $[inout [name]_inout [<column-type> type]]
		
	proc-stmt:
		VariableAssign(name, expr)
			-> $[set @[name] = [<sql-expression> expr];["\r"]]

	proc-stmt:
		BeginEnd(DeclareList(declares), StatementList(stmts)) 
			-> $[begin
				 [<foldout(declare-statement)> (declares, " ")]
				 [<map(proc-stmt)> stmts]
				 end;["\r\r"]]
	proc-stmt:
		IfThen(expr, StatementList(stmts), elseifs, None()) 
			-> $[if [<sql-expression> expr]
				 then [<map(proc-stmt)> stmts]
				 [<foldout(elseif)> (elseifs, "\r")]
				 end if;["\r\r"]]

	proc-stmt:
		IfThen(expr, StatementList(stmts), elseifs, StatementList(stmts')) 
			-> $[if [<sql-expression> expr]
				 then [<map(proc-stmt)> stmts]
				 [<foldout(elseif)> (elseifs, "\r")]
				 else [<map(proc-stmt)> stmts']
				 end if;["\r\r"]]

	proc-stmt:
		Update(uo,tr,[(name, expr)],wp,obp,lp) 
			-> $[
				update [<foldout(update-option)> (uo, " ")] [<table-reference> tr]
				set [name] = [<sql-expression> expr]
				[<maybe-output(where-part)> wp]
				[<maybe-output(order-by-part)> obp]
				[<maybe-output(limit-part)> lp];["\r\r"]]

	proc-stmt:
		Insert( ip , ig, into ,tn,cns, es, od) 
			-> $[insert [<maybe-output(insert-priority)> ip] [<maybe-output(ignore)> ig]
				[<maybe-output(into)> into]
				`[tn]`
				([<foldout(column-name)> (cns, ", ")])
				values
				([<foldout(sql-expression)> (es, ", ")])
				[<maybe-output(on-duplicate)> od];["\r\r"]]

	proc-stmt:
		InsertInto(tn,cns, q) 
			-> $[insert into
				`[tn]`
				([<foldout(column-name)> (cns, ", ")])
				[<sql-expression> q];["\r\r"]]

	proc-stmt:
		Delete( dp , q, ig ,tn,wp,obp, lp) 
			-> $[delete [<maybe-output(delete-priority)> dp] [<maybe-output(quick)> q]
				[<maybe-output(ignore)> ig]
				from [tn]
				[<maybe-output(where-part)> wp]
				[<maybe-output(order-by-part)> obp]
				[<maybe-output(limit-part)> lp];["\r\r"]]

	proc-stmt:
		Select(sts,ses,fp, wp, gbp,hp,obp,lp,pp,ip,mp)
			-> $[[<sql-expression> Select(sts,ses,fp, wp, gbp,hp,obp,lp,pp,ip,mp)];["\r\r"]]

	proc-stmt:
		Return(expr)
			-> $[return [<sql-expression> expr];]
			
	proc-stmt:
		something -> $[] where log(|Error(), "proc-stmt not working", something)		
			
	insert-priority:
		Some(LowPriority()) -> $[low_priority]
		
	insert-priority:
		Some(DelayedPriority()) -> $[delayed]
		
	insert-priority:
		Some(HighPriority()) -> $[high_priority]
	
	delete-priority:
		Some(LowPriority()) -> $[low_priority]

	quick:
		Some(Quick()) -> $[quick]
				
	ignore:
		Some(Ignore()) -> $[ignore]
				
	into:
		Into() -> $[into]
		

	column-name:
		Output(o) -> $[`[o]_out`]
		
	column-name:
		cn -> $[`[cn]`]
		
			
	on-duplicate:
		OnDuplicate(ups)
			-> $[on duplicate key update [<foldout(update-part)> (ups, ", ")]]
			
	update-part:
		UpdatePart(cn, expr)
			-> $[[cn] = [<sql-expression> expr]]


			
			
	elseif:
		ElseIf(expr, StatementList(stmts))	
			-> $[elseif [<sql-expression> expr]
				 then [<map(proc-stmt)> stmts]
				 ]
				 
	update-option:
		LowPriority()
			-> $[LOW_PRIORITY]
			
	update-option:
		Ignore()
			-> $[IGNORE]
			
	table-reference:
		 TableName(name)
		 	-> $[`[name]`]	
		 	
	update:
		(name, expr)
			-> $[`[name]` = [<sql-expression> expr]]

			
	where-part:
		Where(exp)
			-> $[where [<sql-expression> exp]]

			

	limit-part:
		None()
			-> $[]
			
			
	sql-expression:
		Or(exp1, exp2)
			-> $[ ([<sql-expression> exp1] or [<sql-expression> exp2])]

	sql-expression:
		Xor(exp1, exp2)
			-> $[([<sql-expression> exp1] xor [<sql-expression> exp2])]

	sql-expression:
		And(exp1, exp2)
			-> $[([<sql-expression> exp1] and [<sql-expression> exp2])]

	sql-expression:
		Not(exp)
			-> $[ not ([<sql-expression> exp])]
			
	sql-expression:
		Is(bp,Not(),tfu)
			-> $[[<sql-expression> bp] is not [<sql-expression> tfu]]

	sql-expression:
		Is(bp,None(),tfu)
			-> $[[<sql-expression> bp] is [<sql-expression> tfu]]
			
	sql-expression:
		IsNull(bp,Not())
			-> $[[<sql-expression> bp] is not null]

	sql-expression:
		IsNull(bp,None())
			-> $[[<sql-expression> bp] is null]
		
	sql-expression:
		Iff(exp1,exp2)
			-> $[[<sql-expression> exp1] <=> [<sql-expression> exp2]]		
	
	sql-expression:
		Equal(exp1,exp2)
			-> $[[<sql-expression> exp1] <=> [<sql-expression> exp2]]		

	sql-expression:
		GreaterThanEqual(exp1,exp2)
			-> $[[<sql-expression> exp1] >= [<sql-expression> exp2]]		

	sql-expression:
		GreaterThan(exp1,exp2)
			-> $[[<sql-expression> exp1] > [<sql-expression> exp2]]		

	sql-expression:
		LessThanEqual(exp1,exp2)
			-> $[[<sql-expression> exp1] <= [<sql-expression> exp2]]		

	sql-expression:
		LessThan(exp1,exp2)
			-> $[[<sql-expression> exp1] < [<sql-expression> exp2]]		

	sql-expression:
		NotEqual(exp1,exp2)
			-> $[not([<sql-expression> exp1] <=> [<sql-expression> exp2])]		

	sql-expression:
		Equal(exp1, allany, query)
			-> $[[<sql-expression> exp1] = [<all-any> allany] [<query> query]]		

	sql-expression:
		GreaterThanEqual(exp1,allany, query)
			-> $[[<sql-expression> exp1] >= [<all-any> allany] [<query> query]]		

	sql-expression:
		GreaterThan(exp1,allany, query)
			-> $[[<sql-expression> exp1] > [<all-any> allany] [<query> query]]		

	sql-expression:
		LessThanEqual(exp1,allany, query)
			-> $[[<sql-expression> exp1] <= [<all-any> allany] [<query> query]]		

	sql-expression:
		LessThan(exp1,allany, query)
			-> $[[<sql-expression> exp1] < [<all-any> allany] [<query> query]]		

	sql-expression:
		NotEqual(exp1,allany, query)
			-> $[[<sql-expression> exp1] != [<all-any> allany] [<query> query]]		


	sql-expression:
		InQuery(expr, maybenot, query)
			-> $[[<sql-expression> expr] [<maybe-output(maybenot)> maybenot] in [<query> query]]

	sql-expression:
		InSet(expr, maybenot, exprs)
			-> $[[<sql-expression> expr] [<maybe-output(maybenot)> maybenot] in [<foldout(sql-expression)> (exprs, ",")]]
	
	sql-expression:
		Between(expr1, maybenot, expr2, expr3)
			-> $[[<sql-expression> expr1] [<maybe-output(maybenot)> maybenot] between [<sql-expression> expr2] and [<sql-expression> expr3]]

	sql-expression:
		SoundsLike(expr1, expr2)
			-> $[[<sql-expression> expr1] sounds like [<sql-expression> expr2]]

	sql-expression:
		Like(expr1, maybenot, expr2, escape)
			-> $[[<sql-expression> expr1] [<maybe-output(maybenot)> maybenot] like [<sql-expression> expr2] [<maybe-output(maybeescape)> escape]]

	sql-expression:
		Regexp(expr1, maybenot, expr2)
			-> $[[<sql-expression> expr1] [<maybe-output(maybenot)> maybenot] regexp [<sql-expression> expr2]]

	sql-expression:
		BitOr(expr1, expr2)
			-> $[[<sql-expression> expr1] | [<sql-expression> expr2]]
	
	sql-expression:
		BitAnd(expr1, expr2)
			-> $[[<sql-expression> expr1] & [<sql-expression> expr2]]

	sql-expression:
		BitShiftLeft(expr1, expr2)
			-> $[[<sql-expression> expr1] << [<sql-expression> expr2]]

	sql-expression:
		BitShiftRight(expr1, expr2)
			-> $[[<sql-expression> expr1] >> [<sql-expression> expr2]]

	sql-expression:
		Plus(expr1, expr2)
			-> $[[<sql-expression> expr1] + [<sql-expression> expr2]]

	sql-expression:
		Minus(expr1, expr2)
			-> $[[<sql-expression> expr1] - [<sql-expression> expr2]]

	sql-expression:
		Multiply(expr1, expr2)
			-> $[[<sql-expression> expr1] * [<sql-expression> expr2]]

	sql-expression:
		Divide(expr1, expr2)
			-> $[[<sql-expression> expr1] / [<sql-expression> expr2]]

	sql-expression:
		Div(expr1, expr2)
			-> $[[<sql-expression> expr1] div [<sql-expression> expr2]]

	sql-expression:
		Mod(expr1, expr2)
			-> $[[<sql-expression> expr1] mod [<sql-expression> expr2]]

	sql-expression:
		Percent(expr1, expr2)
			-> $[[<sql-expression> expr1] % [<sql-expression> expr2]]

	sql-expression:
		Power(expr1, expr2)
			-> $[[<sql-expression> expr1] ^ [<sql-expression> expr2]]

	sql-expression:
		PlusInt(expr1, interval)
			-> $[[<sql-expression> expr1] + [<interval> interval]]

	sql-expression:
		MinusInt(expr1, interval)
			-> $[[<sql-expression> expr1] - [<interval> interval]]

	sql-expression:
		Collate(expr, collname)
			-> $[[<sql-expression> expr] collate [collname]]
			
	sql-expression:
		Or(expr1, expr2)
			-> $[[<sql-expression> expr1] || [<sql-expression> expr2]]

	sql-expression:
		UnaryPlus(expr)
			-> $[+[<sql-expression> expr]]

	sql-expression:
		UnaryMinus(expr)
			-> $[-[<sql-expression> expr]]

	sql-expression:
		Reverse(expr)
			-> $[~[<sql-expression> expr]]

	sql-expression:
		Not(expr)
			-> $[![<sql-expression> expr]]

	sql-expression:
		Binary(expr)
			-> $[binary [<sql-expression> expr]]

	sql-expression:
		SetExpression(exprs)
			-> $[([<foldout(sql-expression)> (exprs, ", ")])]

	sql-expression:
		Row(expr, exprs)
			-> $[row [<sql-expression> expr], [<foldout(sql-expression)> (exprs, ", ")]]
			
	sql-expression:
		Exists(query)
			-> $[exists [<query> query]]
			
	sql-expression:
		EscapeSyntax(name, expr)
			-> $[{ [name] [<sql-expression> expr]}]
	
	sql-expression:
		String(str)
			-> $['[<<un-double-quote> str <+ !str>]']
			
	
	sql-expression:
		Int(i)
			-> $[[i]]

	sql-expression:
		Decimal(d)
			-> $[[d]]

	sql-expression:
		True() 
			-> $[true]

	sql-expression:
		False() 
			-> $[false]

	sql-expression:
		Unknown() 
			-> $[unknown]

	sql-expression:
		Null()
			-> $[null]
			
	sql-expression:
		ColumnName(name)
			-> $[`[name]`]

	sql-expression:
		TableColumnName(tname, cname)
			-> $[`[tname]`.`[cname]`]

	sql-expression:
		ExpressionAs(expr, i)
			-> $[[<sql-expression> expr] as [i]]

	sql-expression:
		ColumnNameAs(name, i)
			-> $[([name] as [i])]


	sql-expression:
		TableColumnNameAs(tname, cname, i)
			-> $[([tname].[cname] as i)]

	sql-expression:
		Star()
			-> "*"

	sql-expression:
		DBTableColumnName(dname,tname,cname)
			-> $[[dname].[tname].[cname]]

	sql-expression:
		LocalVariable(name)
			-> $[@[name]]

	sql-expression:
		SystemVariable(name)
			-> $[[name]]
	
	sql-expression:
		Marker()
			-> $[?]
			
	sql-expression:
		MatchExpression(ids, expr, modifier)
			->$[match ([<foldout(output-id)> (ids, ", ")]) against ([<sql-expression>expr] [<maybe-output(search-modifier)> modifier])]

	sql-expression:
		CaseExpression(expr, cws, ce)
			->
				$[case [<sql-expression> expr]
				    [<foldout(case-when)> (cws, "\n")]
				    [<maybe-output(case-else)> ce]
				  end]
			
	sql-expression:
		FunctionCall(name, params)
			-> $[[name]([<foldout(sql-expression)> (params, ", ")])]
				
	sql-expression:
		Select(sts,ses,fp, wp, gbp,hp,obp,lp,pp,ip,mp)
			-> $[(select [<select-type> sts] [<foldout(sql-expression)> (ses, " ")] [<maybe-output(from-part)> fp]
			   [<maybe-output(where-part)> wp]
			   [<maybe-output(group-by-part)> gbp]
			   [<maybe-output(having-part)> hp]
			   [<maybe-output(order-by-part)> obp]
			   [<limit-part> lp]
			   [<maybe-output(procedure-part)> pp]
			   [<maybe-output(into-part)> ip]
			   [<maybe-output(mode-part)> mp])]
	

	sql-expression:
		Output(o) -> $[[o]_out]
				
	sql-expression:
		expr -> 
			$[expr]
		where
			log(|Error(), "cannot pretty-print sql-expression:", expr)
		  
	case-when:
		[CaseWhen(expr1, expr2)]
			-> $[when [<sql-expression> expr1] then [<sql-expression> expr2]]
			
	case-else:
		CaseElse(expr) 
			-> $[else [<sql-expression> expr]]
			
		 
	

	output-id:
		[i]
			-> $[[i]]
	
		
	search-modifier:
		InBooleanMode() 
			-> $[in boolean mode]
	
	search-modifier:
		WithQueryExpansion()
			-> $[with query expansion]
			


	maybenot:
		Not() -> $[not]
			
	maybeescape:
		Escape(expr)
			-> $[escape [<sql-expression> expr]]

	interval:
		IntervalExpression(expr, unit)
			-> $[interval [<sql-expression> expr] [<unit> unit]]
			
	unit:
		MicroSecondUnit()
			-> $[microsecond]		

	unit:
		SecondUnit()
			-> $[second]		

	unit:
		MinuteUnit()
			-> $[minute]		

	unit:
		HourUnit()
			-> $[hour]		

	unit:
		DayUnit()
			-> $[day]		

	unit:
		WeekUnit()
			-> $[week]		

	unit:
		MonthUnit()
			-> $[month]		

	unit:
		QuarterUnit()
			-> $[quarter]		

	unit:
		YearUnit()
			-> $[year]		

	unit:
		SecondMicroSecondUnit()
			-> $[second_microsecond]		

	unit:
		MinuteMicroSecondUnit()
			-> $[minute_microsecond]		

	unit:
		MinuteSecondUnit()
			-> $[minute_second]		

	unit:
		HourMicroSecondUnit()
			-> $[hour_microsecond]		

	unit:
		HourSecondUnit()
			-> $[hour_second]		

	unit:
		HourMinuteUnit()
			-> $[hour_minute]		

	unit:
		DayMicroSecondUnit()
			-> $[day_microsecond]		

	unit:
		DaySecondUnit()
			-> $[day_second]		

	unit:
		DayMinuteUnit()
			-> $[day_minute]		

	unit:
		DayHourUnit()
			-> $[day_hour]		

	unit:
		YearMonthUnit()
			-> $[year_month]		


	query:
		_ -> []


	all-any:
		All()
			-> "all"

	all-any:
		All()
			-> "any"

	select-type:
		[] -> ""
		
	select-type:
		[t, ts*] -> $[ [<select-type> t] [<select-type> [ts*]]]

	select-type:
		All() -> "all"

	select-type:
		Distinct() -> "distinct"

	select-type:
		DistinctRow() -> "distinctrow"

	select-type:
		HighPriority() -> "high_priority"

	select-type:
		StraightJoin() -> "straight_join"

	select-type:
		SmallResult() -> "sql_small_result"

	select-type:
		BigResult() -> "sql_big_result"

	select-type:
		BufferResult() -> "sql_buffer_result"

	select-type:
		Cache() -> "sql_cache"

	select-type:
		NoCache() -> "sql_no_cache"

	select-type:
		CalcFoundRows() -> "sql_calc_found_rows"

		
	from-part:
		Some(FromPart(trs))
		->
		$[from [<table-references> trs]]

	table-references:
		[] -> ""

	table-references:
		[tr]
			-> $[[<table-reference> tr]]

	table-references:
		[tr, trs*]
			-> $[[<table-reference> tr], [<table-references> [trs*]]]
			
	table-reference:
		TableName(tn) -> $[[tn]]

	table-reference:
		Aliased(tn, a, ih) 
			-> $[[tn] as [<alias> a] [<index-hint> ih]]
			
	table-reference:
		Query(q,a)
			-> $[<query> q [<alias> a]]

	table-reference:
		Bracket(trs) -> $[([<table-references> trs])]		

	table-reference:
		OJ(tr, tr', expr)
			-> $[oj [<table-reference> tr] left outer join [<table-reference> tr'] on [<sql-expression> expr]]

	table-reference:
		InnerJoin(tr, tr', jc)
			-> $[[<table-reference> tr] inner join [<table-reference> tr'] [<join-condition> jc]]

	table-reference:
		CrossJoin(tr, tr', jc)
			-> $[[<table-reference> tr] cross join [<table-reference> tr'] [<join-condition> jc]]

	table-reference:
		StraightJoin(tr, tr', jc)
			-> $[[<table-reference> tr] straight join [<table-reference> tr'] [<join-condition> jc]]

	table-reference:
		LeftJoin(tr, tr', jc)
			-> $[[<table-reference> tr] left join [<table-reference> tr'] [<join-condition> jc]]

	table-reference:
		LeftOuterJoin(tr, tr', jc)
			-> $[[<table-reference> tr] left outer join [<table-reference> tr'] [<join-condition> jc]]

	table-reference:
		RightJoin(tr, tr', jc)
			-> $[[<table-reference> tr] right join [<table-reference> tr'] [<join-condition> jc]]

	table-reference:
		RightOuterJoin(tr, tr', jc)
			-> $[[<table-reference> tr] right outer join [<table-reference> tr'] [<join-condition> jc]]

	table-reference:
		NaturalLeftJoin(tr, tr', jc)
			-> $[[<table-reference> tr] natural left join [<table-reference> tr'] [<join-condition> jc]]

	table-reference:
		NaturalLeftOuterJoin(tr, tr', jc)
			-> $[[<table-reference> tr] natural left outer join [<table-reference> tr'] [<join-condition> jc]]

	table-reference:
		NaturalRightJoin(tr, tr', jc)
			-> $[[<table-reference> tr] natural right join [<table-reference> tr'] [<join-condition> jc]]

	table-reference:
		NaturalRightOuterJoin(tr, tr', jc)
			-> $[[<table-reference> tr] natural right outer join [<table-reference> tr'] [<join-condition> jc]]



	alias:
		Alias(n)
			-> $[[n]]	


	index-hint:
		[] -> $[]

	index-hint:
		Use(ik, fc)
			-> $[use [<index-key> ik] [<maybe-output(for-clause)> fc]]

	index-hint:
		Ignore(ik, fc)
			-> $[ignore [<index-key> ik] [<maybe-output(for-clause)> fc]]

	index-hint:
		Force(ik, fc)
			-> $[force [<index-key> ik] [<maybe-output(for-clause)> fc]]


	index-key:
		Index() -> "Index"

	index-key:
		Key() -> "Key"
		
	for-clause:
		Some(ForClause(jog, ns))
			-> $[for [<jog> jog] [<foldout(column-name)> (ns, ", ")]]

	jog:
		Join() -> "join"
		
	jog:
		OrderBy() -> "order by"

	jog:
		GroupBy() -> "group by"

	join-condition:
		None()
			-> $[]
			
	join-condition:
		On(expr)
			-> $[on [<sql-expression> expr]]
			
	join-condition:
		Using(ns)
			-> $[using [<foldout(column-name)> (ns, ", ")]]
		
	group-by-part:
		Some(GroupByPart(expr, o, r))
			-> $[group by [<sql-expression> expr] [<maybe-output(order)> o] [<maybe-output(rollup)> r]]
 
 	order:
 		Some(Ascending()) -> "asc"

 	order:
 		Some(Descending()) -> "desc"
 		 		
 	rollup:
 		Some(Rollup())
 			-> "with rollup"

	having-part:
		Some(HavingPart(expr))
			-> $[having [<sql-expression> expr]]
	
	order-by-part:
		Some(OrderByPart(expr, o))
			-> $[order by [<sql-expression> expr] [<maybe-output(order)> o]]
	
			
	procedure-part:
		Some(ProcedurePart(n, ps))
			-> $[procedure [n] ([<foldout(sql-expression)> (ps, ", ")])]
     
    into-part:
    	Some(IntoOutFile(s,cs,eo))
    		-> $[into outfile [s] [<charset> cs] [<maybe-output(export-options)> eo]] 

    into-part:
    	Some(Into(vars))
    		-> $[into [<foldout(sql-expression <+ column-name )> (vars, ", ")]] 


	charset:
		CharSet(name)
			-> $[character set [name]]      
			

	export-options:
		Some(ExportOptions())
			-> "export options unknown"
			
		
	mode-part:
		Some(ForUpdate()) -> "for update"

	mode-part:
		Some(LockShareMode()) -> "lock in share mode"
		
	declare-statement:
		Declare(names, type, default) 
			-> $[declare [<foldout(column-name)> (names, ", ")] [<column-type> type] [<maybe-output(declare-default)> default];]

		
		
	declare-default:
		DefaultDeclare(expr)
			-> $[default [<sql-expression> expr]]	
	


	standard-stuff:
		name ->
			$[]

