module simplify

imports
  include/Booster2


library/lookupTable
translate/booster/booster  
  
rules

	simplify-main:
		system -> system
		where
			simp-programs := <map(\((cname, mname), (constraint, gc, exts, done, ins, outs)) -> <simp> (cname, mname, gc)\)> <LookupTable> "Method"
			
	simp:
		(cname, mname, prog) 
			-> (cname, mname, prog) 
		where 
			prog' := <innermost(conjoin <+ simplify <+ simplify-assume-inv)> prog;
			<updateMethodProgram>(cname, mname, prog')
			
	
	simplify-assume-inv:
		BinRel(Path(ps, PathComponent(attname, None())){BasicType(t)}, 
				In(), 
				TypeExtent(ext){ Set(BasicType(t))})
			-> True() 
	
	simplify-assume-inv:
		BinRel(Path(ps {Reference(classname)}, PathComponent(attname, None())){Set(Reference(classname'))},
				SubsetEquals(),
				PathStart(classname', None()){Set(Reference(classname'))})
			-> True()
		where
			(ds, Set(Reference(classname')), opp, min, max, s) := <lookup>((classname, attname), <LookupTable>"Attribute")

	simplify-assume-inv:
		BinRel(Path(ps {Optional(Reference(classname))}, PathComponent(attname, None())){Set(Reference(classname'))},
				SubsetEquals(),
				PathStart(classname', None()){Set(Reference(classname'))})
			-> True()
		where
			(ds, Set(Reference(classname')), opp, min, max, s) := <lookup>((classname, attname), <LookupTable>"Attribute")

	simplify-assume-inv:
		BinRel(Path(ps {Optional(Reference(classname))}, PathComponent(attname, None())){Reference(classname')},
				In(),
				PathStart(classname', None()){Set(Reference(classname'))})
			-> True()
		where
			(ds, Reference(classname'), opp, min, max, s) := <lookup>((classname, attname), <LookupTable>"Attribute")

			
			
/*	simplify:
		BinRel(expr, Equal(), This())
			-> BinRel(This(), Equal(), expr)
		where
			<not(eq)>(This(), expr)

	simplify:
		BinRel(expr, NotEqual(), This())
			-> BinRel(This(), NotEqual(), expr)
		where
			<not(eq)>(This(), expr)
*/

	simplify:
		Conjuncts([]) -> True()

	simplify:
		Conjuncts([x]) -> x

	simplify:
		Conjuncts(ps) 
			-> Conjuncts(ps')
		where
			cs := <filter( ?Conjuncts(_))> ps;
			ps' := <union>(<diff>(ps, cs), <concat> <map(\ Conjuncts(xs) -> xs\)> cs);
			<not(equal)>(ps, ps')
			
	simplify:
		Parallels(ps) 
			-> Parallels(ps')
		where
			cs := <filter( ?Parallels(_))> ps;
			ps' := <union>(<diff>(ps, cs), <concat> <map(\ Parallels(xs) -> xs\)> cs);
			<not(equal)>(ps, ps')

		

	simplify:
		Or(a, Conjuncts(cs))
			-> a
		where <elem>(a, cs)

	simplify:
		Or(Conjuncts(as), Conjuncts(bs))
			-> Or(Conjuncts(cs), Conjuncts(ds))
		where
			cs := <isect>(as,bs);
			<not(equal)>(cs, []);
			ds := <union>(<diff>(as, cs), <diff>(bs, cs))

	simplify:
		Not(True()) -> False()

	simplify:
		Not(False()) -> True()
		

	simplify:
		Choice(Guard(c1,p1),
		       Guard(c1,p2))
		->
			Guard(c1, Choice(p1,p2))
	
	simplify:
		Choice(Guard(Conjuncts(cs),p1),
		       Guard(Conjuncts(cs'),p2))
		->
			Guard(Conjuncts(cs1), Choice(Guard(Conjuncts(cs2),p1),
		       				 Guard(Conjuncts(cs3),p2)))
		where
			cs1 := <isect>(cs,cs');
			<not(equal)>(cs1, []);
			cs2 := <diff>(cs, cs1);
			cs3 := <diff>(cs', cs1)

/*	simplify:
		Choice(Guard(c1,p1),
		       Guard(Conjuncts(cs),p2))
		->
			Guard(c1, Choice(Guard(True(),p1),
		       				 Guard(Conjuncts(cs'),p2)))
		where
			<elem>(c1, cs);
			cs' := <diff>(cs, [c1])

	simplify:
		Choice(Guard(Conjuncts(cs),p1),
		       Guard(c1,p2))
		->
			Guard(c1, Choice(Guard(Conjuncts(cs'),p1),
		       				 Guard(True(),p2)))
		where
			<elem>(c1, cs);
			cs' := <diff>(cs, [c1])
*/

	simplify:
		Choice(Parallels(as), Parallels(bs))
			-> Parallels(<union>(cs, Choice(Parallels(ds), Parallels(es))))
		where
			cs := <isect>(as,bs);
			<not(equal)>(cs, []);
			ds := <diff>(as, cs);
			es := <diff>(bs, cs)

/*	simplify:
		Choice(Guard(g1, Parallels(as)), Guard(Not(g1), Parallels(bs)))
			-> Parallels(<union>(cs, [Choice(Guard(g1, Parallels(ds)), Guard(Not(g1), Parallels(es)))]))
		where
			cs := <isect>(as,bs);
			<not(equal)>(cs, []);
			ds := <diff>(as, cs);
			es := <diff>(bs, cs)
*/
	simplify:
		Choice(Guard(g1, Parallels(as)), Guard(g2, Parallels(bs)))
			-> Parallels(<union>([Guard(Or(g1, g2), Parallels(cs))], [Choice(Guard(g1, Parallels(ds)), Guard(g2, Parallels(es)))]))
		where
			cs := <isect>(as,bs);
			<not(equal)>(cs, []);
			ds := <diff>(as, cs);
			es := <diff>(bs, cs)


/*	simplify:
		Choice(Guard(g1, Parallels(as)),
			   Guard(Not(g1), a))
		->
			Parallels([a, Choice(Guard(g1, Parallels(bs)),Guard(Not(g1), Skip()))])
		where
			<elem> (a, as);
			bs := <diff>(as, [a])

	simplify:
		Choice(Guard(g1, a),
			   Guard(Not(g1), Parallels(as)))
		->
			Parallel(a, Choice(Guard(g1, Skip()),Guard(Not(g1), Parallels(bs))))
		where
			<elem> (a, as);
			bs := <diff>(as, [a])
*/

/*	simplify:
		Choice(Guard(And(a,b), p),
			   Guard(And(a,c), q))
		-> Guard(a, Choice(Guard(b,p), 
						   Guard(c,q)))

	simplify:
		Choice(Guard(And(b,a), p),
			   Guard(And(c,a), q))
		-> Guard(a, Choice(Guard(b,p), 
					       Guard(c,q)))

	simplify:
		Choice(Guard(And(b,a), p),
			   Guard(a, q))
		-> Guard(a, Choice(Guard(b,p), 
					       q))

	simplify:
		Choice(Guard(And(a,b), p),
			   Guard(a, q))
		-> Guard(a, Choice(Guard(b,p), 
					       q))

	simplify:
		Choice(Guard(a, p),
			   Guard(And(c,a), q))
		-> Guard(a, Choice(p, 
					       Guard(c,q)))
*/

/*			
	simplify:
		Guard(c, Choice(Guard(Not(c), p),
						q))
		-> Guard(c, q)

	simplify:
		Guard(Not(c), Choice(Guard(c, p),
						q))
		-> Guard(c, q)

	simplify:
		Guard(c, Choice(q,
						Guard(Not(c), p)))
		-> Guard(c, q)

	simplify:
		Guard(Not(c), Choice(q,
							 Guard(c, p)))
		-> Guard(c, q)
*/

/*	simplify:
		Guard(c, Parallel(Choice(p, Guard(Not(c), q)), r))
			-> Guard(c, Parallel(Choice(p,q), r))	
			
	simplify:
		Guard(Not(c), Choice(Guard(c,p), r))
			-> Guard(Not(c), Choice(Guard(False(),p), r))

*/	simplify:
		Guard(Not(c), Choice(Guard(c,p), r))
			-> Guard(Not(c), Choice(Guard(False(),p), r))
/*
	simplify:
		Guard(Not(c), Choice(r, Guard(c,p)))
			-> Guard(Not(c), Choice(r, Guard(False(),p)))

	simplify:
		Guard(c, Choice(r, Guard(Not(c),p)))
			-> Guard(Not(c), Choice(r, Guard(False(),p)))
*/

	simplify:
		Choice(Assign(p,e), q)
			-> Assign(p,e)
			
/*	simplify:
		Choice(Guard(g1, Parallel(Parallel(p1, p2), p3)),
			   Guard(Not(g2), p1))
		-> Parallel(p1, Choice(Guard(g1, Parallel(p2,p3)), Guard(Not(g1), Skip())))		
		
	simplify:
		Choice(Parallel(b,a),
			   Parallel(c,a))
		->
			Parallel(a, Choice(b,c))
*/	
	simplify:
		Choice(Guard(False(), p), q)
			-> q

	simplify:
		Choice(p, Guard(False(), q))
			-> p
	
	/* conjoin nested guards */
	/* a--> b--> c -> a & b --> c*/
	simplify:
		Guard(c,Guard(c2,p))
			-> Guard(And(c,c2),p)
			
	/* a [] a -> a */
	simplify:
		Choice(a,a)
			-> a
			
	/* a --> skip -> skip */
	
	simplify:
		Guard(a,Skip())
			-> Skip()
			
	/* True() --> p -> p */
	
	simplify:
		Guard(True(),p)
			-> Skip()
			
	/* Choice elimination. a --> c [] b --> c -> a or b --> c  */
	
	simplify:
		Choice(Guard(a,c),Guard(b,c))
			-> Guard(Or(a,b),c)
	

	simplify:
		Conjuncts(cs)
			-> Conjuncts(cs')
		where
			<elem>(True(), cs);
			cs' := <diff>(cs, [True()]) 
			

	simplify:
		Conjuncts(cs)
			-> False()
		where
			<elem>(False(), cs)

	simplify:
		Conjuncts(cs)
			-> Conjuncts(cs')
		where
			cs' := <nub> cs;
			<not(equal)> (cs, cs')
			
	simplify:
		Or(constraint, True())
			-> True()
	
	simplify:
		Or(True(), constraint)
			-> True()

	simplify:
		Or(constraint, constraint)
			-> constraint

	simplify:
		Or(constraint, Not(constraint))
			-> True()

	
	simplify:
		Conjuncts(ps)
			-> Conjuncts(ps')
		where
			imps := <filter(\ Implies(p,q) -> Implies(p,q) where <elem>(q, ps) \)> ps;
			ps' := <diff>(ps, imps);
			<not(equal)>(ps, ps') 
			
	
/*		And(Implies(p,q),q)
			-> q
*/	
/*	simplify:
		And(And(r, Implies(p,q)),q)
			-> And(r,q)
*/	
		
	simplify:
		Implies(True(), constraint)
			-> constraint

	simplify:
		Implies(False(), constraint)
			-> True()

	simplify:
		Implies(constraint, True())
			-> True()
			
	simplify:
		Implies(constraint, False())
			-> Not(constraint)
			
	simplify:
		Implies(p, Implies(p, constraint))
			-> Implies(p, constraint)

	/* Is this true if there is a primed attribute in the constraint? For that one it would be the constraint itself? */
	simplify:
		Implies(constraint, constraint)
			-> True()


	simplify:
		Implies(
			BinRel(a, Equal(), b),
			Implies(
				BinRel(a, NotEqual(), b),
				constraint
		)) -> Implies(False(), constraint)

	simplify:
		Implies(
			BinRel(a, NotEqual(), b),
			Implies(
				BinRel(a, Equal(), b),
				constraint
		)) -> Implies(False(), constraint)
	
/*	simplify:
		Or(p, And(Not(p), q))
			-> Or(p,q) */

	simplify:
		Or(Conjuncts(ps), Not(Conjuncts(qs)))
			-> Or(Conjuncts(ps'), Not(Conjuncts(qs')))
		where
			<not(equal)> (<isect>(qs, ps), []);
			ps' := <diff>(ps, qs);
			qs' := <diff>(qs, ps)

	simplify:
		Or(Not(Conjuncts(ps)), Conjuncts(qs))
			-> Or(Not(Conjuncts(ps')), Conjuncts(qs'))
		where
			<not(equal)> (<isect>(qs, ps), []);
			ps' := <diff>(ps, qs);
			qs' := <diff>(qs, ps)
			
				
	simplify:
		Guard(True(), program)
			-> program
						
	simplify:
		And(constraint, constraint)
			-> constraint

	simplify:
		And(constraint, And(constraint1, constraint) )
			-> And(constraint1, constraint)
			
	simplify:
		And(constraint, And(constraint, constraint1) )
			-> And(constraint1, constraint)
			
	simplify:
		Or(constraint, False())
			-> constraint

	simplify:
		Or(False(), constraint)
			-> constraint
	
	/* False & ...*/
	simplify:
		And(constraint, False())
			-> False()

	simplify:
		And(False(), constraint)
			-> False()
	/**/
	
	simplify:
		And(And(constraint, constraint1), constraint  )
			-> And(constraint1, constraint)

	simplify:
		And(And(constraint1, constraint), constraint  )
			-> And(constraint1, constraint)
	
	/* True & a & b -> a & b*/
	simplify:
		And(True(), And(constraint, constraint1))
			-> And(constraint, constraint1)
			
	simplify:
		And(And(constraint, constraint1),True())
			-> And(constraint, constraint1)
	
	/* True & a -> a*/
	simplify:
		And(True(), constraint)
			-> constraint
			
	simplify:
		And(constraint, True())
			-> constraint
		
	
			
/*			
	simplify:
		And(BinRel(expression1, Equal(), expression2), BinRel(expression1, NotEqual(), expression2))
			-> False()

	simplify:
		And(BinRel(expression1, NotEqual(), expression2), BinRel(expression1, Equal(), expression2))
			-> False()
			
	simplify:
		Implies(And(BinRel(e1, Equal(), e2), BinRel(e3, Equal(), e4)), BinRel(e3, NotEqual(), e4))
			-> Or(BinRel(e1, NotEqual(), e2),BinRel(e3,NotEqual(), e4)) 

	simplify:
		Implies(And(BinRel(e1, Equal(), e2), BinRel(e3, NotEqual(), e4)), BinRel(e3, Equal(), e4))
			-> Or(BinRel(e1, NotEqual(), e2),BinRel(e3, Equal(), e4)) 
			
	simplify:
		Or(
			And(
				BinRel(expression1, NotEqual(), expression2),
				BinRel(expression1, NotEqual(), expression3)),
			Or(BinRel(expression1, Equal(), expression2),
			   BinRel(expression1, Equal(), expression3)))
		-> True()
			
	simplify:
		Or(
			And(
				BinRel(expression1, NotEqual(), expression2),
				BinRel(expression1, NotEqual(), expression3)),
			Or(
				BinRel(expression1, Equal(), expression3),
				BinRel(expression1, Equal(), expression2)))
		-> True()
*/		
	simplify:
		Forall(name, expr, True())
			-> True()
			
	/* here we should really do a literal substitute - name for expr2 */
	simplify:
		Forall(name, expr, Implies(BinRel(PathStart(name, None()), Equal(), expr2), constraint))
			-> constraint
					
			
/*	simplify:
		Not(And(a, b))
			-> Or(Not(a), Not(b))
*/
	simplify:
		Not(Or(a, b))
			-> And(Not(a), Not(b))
			
	
	simplify:
		Not(Implies(a,b))
			-> And(a, Not(b))		
			
	simplify:
		BinRel(BasicValue(CurrentDateTime()), Equal(), Null())
			-> False()
					
/*	simplify:
		And(Implies(a,c),Implies(b,c))
			->
				Implies(Or(a,b),c) */
				
/*	simplify:
		Implies(p, q)
			-> Or(Not(p), q)
*/

/*	simplify:
		And(Or(a,b), c)
			-> Or(And(a,c), And(b,c))
*/

	simplify:
		BinRel(expr, NotEqual(), expr)
			-> False()

	simplify:
		BinRel(expr, Equal(), expr)
			-> True()
	
	simplify:
		BinRel(BinOp(a, Plus(), BasicValue(Integer(i))), LessThanEquals(), BasicValue(Integer(j)))
			-> BinRel(a, LessThanEquals(), BasicValue(Integer(k)))
		where
			k := <subt>(<string-to-int>j,<string-to-int>i)

	simplify:
		BinRel(BinOp(a, Plus(), BasicValue(Integer(i))), LessThan(), BasicValue(Integer(j)))
			-> BinRel(a, LessThan(), BasicValue(Integer(k)))
		where
			k := <subt>(<string-to-int>j,<string-to-int>i) 


	simplify:
		BinRel(BinOp(a, Plus(), BasicValue(Integer(i))), GreaterThanEquals(), BasicValue(Integer(j)))
			-> BinRel(a, GreaterThanEquals(), BasicValue(Integer(k)))
		where
			k := <subt>(<string-to-int>j,<string-to-int>i)

	simplify:
		BinRel(BinOp(a, Plus(), BasicValue(Integer(i))), GreaterThan(), BasicValue(Integer(j)))
			-> BinRel(a, GreaterThan(), BasicValue(Integer(k)))
		where
			k := <subt>(<string-to-int>j,<string-to-int>i)


	simplify:
		BinRel(expression1, In(), BinOp(expression2, Union(), SetExtent([expression1])))
			-> True()

	simplify:
		BinRel(expression1, NotIn(), BinOp(expression2, Union(), SetExtent([expression1])))
			-> BinRel(expression1, NotIn(), expression2)
			

	simplify:
		BinRel(BinOp(expression1, Union(), expression2), SubsetEquals(), expression3)
			-> And(BinRel(expression1, SubsetEquals(), expression3), 
					BinRel(expression2, SubsetEquals(), expression3))

	

	simplify:
		BinRel(SetExtent([]), SubsetEquals(), expression2)
			-> True()		

	simplify:
		BinRel(SetExtent([exp, exps*]), SubsetEquals(), expression2)
			-> And(BinRel(exp, In(), expression2), BinRel(SetExtent([exps*]), SubsetEquals(), expression2))

			
	simplify:
		Not(BinRel(e1, Equal(), e2))
		 	-> BinRel(e1, NotEqual(), e2)		

	simplify:
		Not(BinRel(e1, NotEqual(), e2))
		 	-> BinRel(e1, Equal(), e2)		
		 	
/*	simplify:
		Not(BinRel(e1, In(), e2))
		 	-> BinRel(e1, NotIn(), e2)		

	simplify:
		Not(BinRel(e1, NotIn(), e2))
		 	-> BinRel(e1, In(), e2)	
*/		 	
	

	simplify:
		(BinOp(BasicValue(Integer(i)), Times(), BasicValue(Integer(j))))
			-> BasicValue(Integer( k ))
		where
			k := <mulS>(<string-to-int>i, <string-to-int>j)

			
	simplify:
		(BinOp(BasicValue(Integer(i)), Plus(), BasicValue(Integer(j))))
			-> BasicValue(Integer( k ))
		where
			k := <addS>(<string-to-int>i, <string-to-int>j)

	simplify:
		(BinOp(BasicValue(Integer(i)), Minus(), BasicValue(Integer(j))))
			-> BasicValue(Integer( k ))
		where
			k := <subtS>(<string-to-int>i, <string-to-int>j)

	simplify:
		(BinOp(BasicValue(Integer(i)), Divide(), BasicValue(Integer(j))))
			-> BasicValue(Integer( k ))
		where
			k := <divS>(<string-to-int>i,<string-to-int>j)

			
	simplify:
		UnOp(Cardinality(),BinOp(a,Union(), b))
			-> BinOp(UnOp(Cardinality(),a), Plus(), UnOp(Cardinality(),b))
	
	simplify:
		UnOp(Cardinality(),SetExtent([]))
			-> BasicValue(Integer(0))	

	// F - {this.f} <<: F
	
	simplify:
		BinRel(BinOp(exp1, Minus(), exp2), SubsetEquals(), exp1) -> True()

	simplify:
		BinRel(BinOp(exp1, Minus(), exp2), Subset(), exp1) -> True()
	
	simplify:
		BinRel(BinOp(exp1, Minus(), exp2), Subset(), exp3) -> BinRel(exp1, Subset(), exp3)
	
	
	simplify:
		BinRel(exp1, SubsetEquals(), exp1) -> True()

	simplify:
		BinRel(exp1, SupersetEquals(), exp1) -> True()
		
	
	

	// if n <= 0 and 
	//     card(something) >= n
	// then True
	//
	// The cardinality of anything is always greater than 0
	simplify:
		BinRel(UnOp(Cardinality(),something), GreaterThanEquals(), BasicValue(Integer(v)))
			-> True()
		where
			<leq>(v,0)	

/*	simplify:
		BinRel(UnOp(Cardinality(),something), GreaterThan(), BasicValue(Integer(v)))
			-> True()
		where
			<lt>(v,0) */	

	simplify:
		BinRel(UnOp(Cardinality(),something), LessThanEquals(), BasicValue(Integer(v)))
			-> False()
		where
			<leq>(v,0)	

	simplify:
		BinRel(UnOp(Cardinality(),something), LessThan(), BasicValue(Integer(v)))
			-> False()
		where
			<lt>(v,0)	


	simplify:
		BinRel(BinOp(expr, Plus(), expr'), Equal(), BinOp(expr, Plus(), expr''))
			-> BinRel(expr', Equal(), expr'')


/*	simplify:
		Cardinality(SetExtent([a]))
			-> BasicValue(Integer(1))
*/			
	simplify:
		UnOp(Cardinality(),SetExtent([a, as*]))
			-> BasicValue(Integer(k)){BasicType(Int())}
		where
			BasicValue(Integer(i)) := <simplify> UnOp(Cardinality(),SetExtent([as*]));
			k := <add>(i,1)
			

	simplify:
		BinRel(BasicValue(Integer(x)),Equal(), BasicValue(Integer(y)))
			-> False()
		where
			<not(equal)>(x,y)
			
	simplify:
		Implies(BinRel(a, Equal(), b), Implies(BinRel(b, Equal(), a), q))
			-> Implies(BinRel(a, Equal(), b), Implies(BinRel(a, Equal(), b), q))

	simplify:
		Implies(BinRel(a, NotEqual(), b), Implies(BinRel(b, NotEqual(), a), q))
			-> Implies(BinRel(a, NotEqual(), b), Implies(BinRel(a, NotEqual(), b), q))
	
/*	simplify:
		And(Implies(BinRel(a, Equal(), b), c), Implies(BinRel(b, Equal(), a), c))
			-> Implies(BinRel(a, Equal(), b), c)
*/				
	simplify:
		Guard(e1, Guard(e2, p))
			-> Guard(And(e1,e2),p)

	simplify:
		Guard(True(), p)
			-> p
			
	simplify:
		Path(Path(ps {Reference(cname)}, PathComponent(aname1, maybePrimed)){Reference(cname')},PathComponent(aname2, maybePrimed)){Reference(cname)}
			-> ps {Reference(cname)}
			where
				(cname', aname2) := <lookup-opposite>(cname, aname1)
				
	simplify:
		Path(Path(ps {Reference(cname)}, PathComponent(aname1, maybePrimed)){Optional(Reference(cname'))},PathComponent(aname2, maybePrimed)){Optional(Reference(cname))}
			-> ps {Reference(cname)}
			where
				(cname', aname2) := <lookup-opposite>(cname, aname1)
				
	simplify:
		Path(Path(ps {Reference(cname)}, PathComponent(aname1, maybePrimed)){Reference(cname')},PathComponent(aname2, maybePrimed)){Optional(Reference(cname))}
			-> ps {Reference(cname)}
			where
				(cname', aname2) := <lookup-opposite>(cname, aname1)
				
	simplify:
		Path(Path(ps {Reference(cname)}, PathComponent(aname1, maybePrimed)){Optional(Reference(cname'))},PathComponent(aname2, maybePrimed)){Reference(cname)}
			-> ps {Reference(cname)}
			where
				(cname', aname2) := <lookup-opposite>(cname, aname1)
	
	simplify:
		Parallel(Guard(c, g), g')
			-> Guard(c,Parallel(g,g'))
				
	simplify:
		Parallels(ps) 
			-> Parallels(ps')
		where
			<elem>(Skip(), ps);
			ps' := <diff>(ps, [Skip()])
	
	simplify:
		Parallels([])
			-> Skip()


	simplify:
		Parallels([p])
			-> p
					
				
	simplify:
		UnOp(Cardinality(), SetExtent([_]))
			-> BasicValue(Integer(1)) {BasicType(Int())}	
	
	simplify:
		BinRel(BinOp(exp, Plus(), BasicValue(Integer(i))), GreaterThanEquals(), BasicValue(Integer(0)))
			-> True()
		where
			<gt>(i,0)
			
	simplify:
		BinRel(expr1, In(), SetExtent(exprs))
			-> True()
			where
				<elem>(expr1, exprs)

	simplify:
		BinRel(expr1, NotIn(), SetExtent(exprs))
			-> False()
			where
				<elem>(expr1, exprs)
	
	simplify:
		Or(BinRel(expr1, Equal(), expr2),BinRel(expr1, NotEqual(), expr2))
			-> True()

	simplify:
		Or(BinRel(expr1, NotEqual(), expr2),BinRel(expr1, Equal(), expr2))
			-> True()


	simplify:
		Or(BinRel(expr1, In(), expr2),BinRel(expr1, NotIn(), expr2))
			-> True()
			
	simplify:
		Or(BinRel(expr1, In(), expr2),And(BinRel(expr1, NotIn(), expr2), constraint))
			-> Or(BinRel(expr1, In(), expr2), constraint)
			
	simplify:
		Or(BinRel(expr1, In(), SetExtent(exprs)), BinRel(expr1, Equal(), expr2))
			-> BinRel(expr1, In(), SetExtent(exprs))
		where
			<elem>(expr2, exprs)
			

	simplify:
		Conjuncts(ps)
			-> False()
		where
			cs := <concat><filter(\Not(Conjuncts(qs)) -> qs\)> ps;
			<not(equal)>(cs, []);
			<subseteq>(cs, ps)
	
			 
/*			
	simplify:
		Or(And(And(a,b),c),
		   And(Not(And(a,c)),b))
		   	-> b

	simplify:
		Or(And(And(And(d,a),b),c),
		   And(And(d,Not(And(a,c))),b))
		   	-> And(d,b)
*/

/*	simplify:
		Choice(Guard(And(And(a,b),c), p1),
		   	   Guard(And(Not(And(a,c)),b), p2))
		   	-> Guard(b, Choice(Guard(And(a,c), p1),
		   					   Guard(Not(And(a,c)), p2)))
*/
		   	
/*	simplify:
		Or(And(expr1, expr2), And(epx2, expr3))
			-> And(Or(expr1, expr3), expr2)
*/	
	simplify:
		(Then(True(), x)) -> x		

	simplify:
		(Then(x, True())) -> x		
		

	// Some normalisation rules?
			
	 simplify:
		Conjuncts(ps)
			-> Conjuncts(ps')
		where
			ps' := <qsort(\(p,q) -> <string-lt>(<constraint> p, <constraint>q) \)>ps;
			<not(equal)>(ps, ps')

	 simplify:
		Parallels(ps)
			-> Parallels(ps')
		where
			ps' := <qsort(\(p,q) -> <string-lt>(<guardedCommand> p, <guardedCommand>q) \)>ps;
			<not(equal)>(ps, ps')
	
	simplify:
		BinRel(a, Equal(), b)
			-> BinRel(b, Equal(), a)
		where
			<string-lt>(<expression>b, <expression>a);
			<not(equal)>(Null(), b)

	simplify:
		BinRel(a, NotEqual(), b)
			-> BinRel(b, NotEqual(), a)
		where
			<string-lt>(<expression>b, <expression>a);
			<not(equal)>(Null(), b)

/*


	simplify:
		Or(a, b)
			-> Or(b, a)
		where
			<string-lt>(<constraint>b, <constraint>a)
		where
			log(|Error(), "Normalisation 7")

	simplify:
		Or(Or(a, b), c)
			-> Or(Or(a, c), b)
		where
			<string-lt>(<constraint>c, <constraint>b)
		where
			log(|Error(), "Normalisation 8")

	
	
			
	simplify:
		Parallel(p, Guard(c, q))
			-> Guard(c, Parallel(p,q))
		where
			log(|Error(), "Normalisation 11")
			
	simplify:
		Implies(a, b) -> Or(b, Not(a))
		where
			log(|Error(), "Normalisation 12")
*/
	simplify:
		Parallels(ps)
			-> Guard(Conjuncts(gs), Parallels(ps'))
		where
			gs := <filter(\ Guard(g, p) -> g \)> ps;
			<not(equal)>(gs, []);
			ps' := <filter((\ Guard(g, p) -> p \) <+ id)> ps
			


conjoin:
	And(p,q) -> Conjuncts(<union>([p],[q]))		

/* conjoin:
	Or(p,q) -> Disjuncts(<union>([p],[q]))		
*/

conjoin:
	Parallel(p,q) -> Parallels(<union>([p],[q]))		

conjoin:
	And(Conjuncts(p), Conjuncts(q))
		-> Conjuncts(<union>(p, q))
								
conjoin:
	And(Conjuncts(p), q)
		-> Conjuncts(<union>(p, [q]))						
		
conjoin:
	And(q, Conjuncts(p))
		-> Conjuncts(<union>(p, [q]))								
		